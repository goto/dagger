import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        maven {
            url 'https://repo1.maven.org/maven2'
        }
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:6.1.0'
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4.17.0"
    }
}

plugins {
    id 'maven-publish'
    id 'com.jfrog.artifactory' version '4.17.0'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
}


def flinkVersion = rootProject.flinkVersion

version = rootProject.file('version.txt').text.trim()

def minimalVersion = version
def dependenciesVersion = "0.4.0"

description = """dagger to the heart!"""


java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

def mainClassName = "com.gotocompany.dagger.core.KafkaProtoSQLProcessor"

configurations {
    minimalJar
    dependenciesJar

    dependenciesJar.exclude group: 'org.apache.flink', module: 'force-shading'
    dependenciesJar.exclude group: 'com.google.code.findbugs', module: 'jsr305'
    dependenciesJar.exclude group: 'org.apache.commons', module: 'commons-math3'
    dependenciesJar.exclude group: 'org.slf4j'
    dependenciesJar.exclude group: 'log4j'

    compile.extendsFrom minimalJar
    testCompile.extendsFrom compileOnly
    testRuntimeOnly.extendsFrom runtimeOnly
}

// minimalJar: add all dependencies to be included in dagger cluster user jar
// dependenciesJar: add all dependencies to be included in flink docker image
// implementation: add all dependencies required just for local build
// testImplementation: add all dependencies required just for tests
dependencies {
    minimalJar project(path: ':dagger-common', configuration: 'minimalCommonJar')
    minimalJar project(path: ':dagger-functions', configuration: 'minimalFunctionsJar')
    minimalJar('com.gotocompany:depot:0.6.0') {
        exclude group: 'com.google.cloud', module: 'google-cloud-bigtable'
        exclude group: 'com.google.cloud', module: 'google-cloud-bigquerystorage'
        exclude group: 'com.google.cloud', module: 'google-cloud-bigquery'
        exclude group: 'com.google.protobuf', module: 'protobuf-java'
        exclude group: 'com.google.protobuf', module: 'protobuf-java-util'
        exclude group: "io.grpc"
    }
    minimalJar('com.gotocompany:stencil:0.6.0') {
        exclude group: 'com.google.protobuf', module: 'protobuf-java'
        exclude group: 'com.google.protobuf', module: 'protobuf-java-util'
    }
    compileOnly 'org.projectlombok:lombok:1.18.8'
    annotationProcessor 'org.projectlombok:lombok:1.18.8'

    implementation group: 'org.slf4j', name: 'slf4j-reload4j', version: '2.0.7'
    implementation 'org.apache.flink:flink-streaming-java_2.11:' + flinkVersion
    implementation 'org.apache.flink:flink-clients_2.11:' + flinkVersion
    implementation 'org.apache.flink:flink-table:' + flinkVersion
    implementation 'org.apache.flink:flink-table-api-java-bridge_2.11:' + flinkVersion
    implementation 'org.apache.flink:flink-table-planner_2.11:' + flinkVersion

    dependenciesJar 'io.grpc:grpc-all:1.55.1'
    dependenciesJar project(path: ':dagger-common', configuration: 'dependenciesCommonJar')
    dependenciesJar project(path: ':dagger-functions', configuration: 'dependenciesFunctionsJar')
    dependenciesJar(group: 'com.google.cloud', name: 'google-cloud-bigquerystorage', version: '2.39.1') {
        exclude group: "io.grpc"
    }
    dependenciesJar(group: 'com.google.cloud', name: 'google-cloud-bigtable', version: '2.24.1') {
        exclude group: "io.grpc"
    }
    dependenciesJar(group: 'com.google.cloud', name: 'google-cloud-bigquery', version: '2.29.0') {
        exclude group: "io.grpc"
    }
    dependenciesJar 'org.apache.flink:flink-connector-kafka_2.11:' + flinkVersion
    dependenciesJar 'com.google.protobuf:protobuf-java:3.23.2'
    dependenciesJar 'com.google.protobuf:protobuf-java-util:3.1.0'
    dependenciesJar 'org.influxdb:influxdb-java:2.8'
    dependenciesJar 'org.elasticsearch.client:elasticsearch-rest-client:6.6.1'
    dependenciesJar 'com.google.cloud.bigtable:bigtable-hbase-2.x:2.10.0'
    dependenciesJar 'org.asynchttpclient:async-http-client:2.10.1'
    dependenciesJar 'io.vertx:vertx-pg-client:3.9.0'
    dependenciesJar 'org.apache.commons:commons-pool2:2.4.3'
    dependenciesJar 'org.apache.parquet:parquet-protobuf:1.12.2'

    testImplementation project(':dagger-common').sourceSets.test.output
    testImplementation 'junit:junit:4.13.1'
    testImplementation 'org.apache.flink:flink-test-utils_2.11:' + flinkVersion
    testImplementation 'org.apache.kafka:kafka-clients:2.6.3'
    testImplementation 'com.github.tomakehurst:wiremock-standalone:2.27.0'
    testImplementation 'org.jmockit:jmockit:1.25'
    testImplementation 'org.mockito:mockito-core:2.25.1'
    testImplementation 'io.grpc:grpc-all:1.55.1'
    testImplementation 'org.powermock:powermock-module-junit4:2.0.0-beta.5'
    testImplementation 'org.powermock:powermock-api-mockito2:2.0.0-beta.5'
    testImplementation 'com.google.guava:guava:30.0-jre'
    testImplementation 'org.grpcmock:grpcmock-junit5:0.5.0'
    testImplementation 'com.github.stefanbirkner:system-rules:1.19.0'
}

test {
    finalizedBy jacocoTestReport
    jvmArgs '-Djdk.attach.allowAttachSelf=true'
    testLogging {
        events "passed", "skipped", "failed"
    }
}

jacocoTestReport {
    reports {
        xml.enabled false
        html.enabled true
        csv.enabled false
    }
    finalizedBy jacocoTestCoverageVerification
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.87
            }
        }
    }
}

sourceSets {
    main.compileClasspath += configurations.minimalJar
    main.compileClasspath += project(":dagger-common").configurations.minimalCommonJar
    main.compileClasspath += project(":dagger-common").configurations.dependenciesCommonJar
    main.compileClasspath += configurations.dependenciesJar
    main.runtimeClasspath += configurations.minimalJar
    main.runtimeClasspath += project(":dagger-common").configurations.minimalCommonJar
    main.runtimeClasspath += project(":dagger-common").configurations.dependenciesCommonJar
    main.runtimeClasspath += configurations.dependenciesJar

    test.compileClasspath += configurations.minimalJar
    test.compileClasspath += configurations.dependenciesJar
    test.runtimeClasspath += configurations.minimalJar
    test.runtimeClasspath += configurations.dependenciesJar

}

jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    zip64 true
    from {
        (configurations.compileClasspath).collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
    manifest {
        attributes("Main-Class": "$mainClassName")
    }
}

tasks.register('minimalJar', ShadowJar) {
    manifest.attributes 'Main-Class': mainClassName
    archiveClassifier = 'minimal'
    zip64 true
    exclude('META-INF/INDEX.LIST', 'META-INF/*.SF', 'META-INF/*.DSA', 'META-INF/*.RSA')
    from sourceSets.main.output
    configurations = [project.configurations.minimalJar]
    archiveVersion = minimalVersion
    exclude("core-site.xml")
}

tasks.register('dependenciesJar', ShadowJar) {
    mergeServiceFiles()
    manifest.attributes 'Main-Class': mainClassName
    archiveClassifier = 'dependencies'
    zip64 true
    exclude('META-INF/INDEX.LIST', 'META-INF/*.SF', 'META-INF/*.DSA', 'META-INF/*.RSA')
    configurations = [project.configurations.dependenciesJar]
    archiveVersion = dependenciesVersion
}

tasks.register('fatJar', ShadowJar) {
    description = "Builds a executable jar"
    manifest.attributes 'Main-Class': mainClassName
    archiveClassifier = 'fat'
    from(project.convention.getPlugin(JavaPluginConvention).sourceSets.main.output)
    configurations = [project.configurations.runtimeClasspath, project.configurations.minimalJar, project.configurations.dependenciesJar]
    exclude('META-INF/INDEX.LIST', 'META-INF/*.SF', 'META-INF/*.DSA', 'META-INF/*.RSA')
    zip64 true
    mergeServiceFiles()
    append('reference.conf')
}

private Properties loadEnv() {
    Properties properties = new Properties()
    properties.load(new FileInputStream(file("${projectDir}/env/local.properties")));
    properties
}

publishing {
    publications {
        minimalArtifact(MavenPublication) {
            artifact file("$buildDir/libs/dagger-core-${minimalVersion}-minimal.jar")
            groupId project.group
            artifactId project.name
            version = minimalVersion + '-minimal'
        }
        dependenciesArtifact(MavenPublication) {
            artifact file("$buildDir/libs/dagger-core-${dependenciesVersion}-dependencies.jar")
            groupId project.group
            artifactId project.name
            version = dependenciesVersion + '-dependencies'
        }
    }

    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/goto/dagger"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}

artifactory {
    publish {
        defaults {
            publications('mavenJava')
            properties = ['qa.level': 'basic', 'dev.team': 'core']

            publishBuildInfo = true
            publishArtifacts = true
            publishPom = true
        }
    }

    clientConfig.setIncludeEnvVars(true)
    clientConfig.info.setBuildName('dagger')
    clientConfig.info.setBuildNumber(System.env.BUILD_NUMBER)
}

task minimalPublish(dependsOn: 'minimalJar') {
    dependsOn('publishMinimalArtifactPublicationToGitHubPackagesRepository')
    description('Publishes minimal jar')
}

task dependenciesPublish(dependsOn: 'dependenciesJar') {
    dependsOn('publishDependenciesArtifactPublicationToGitHubPackagesRepository')
    description('Publishes dependencies jar')
}

project.afterEvaluate {
    tasks.withType(PublishToMavenLocal) {
        dependsOn minimalJar, dependenciesJar
    }
}


task runFlink(type: JavaExec, dependsOn: classes) {
    Properties properties = loadEnv()
    systemProperties['ConfigSource'] = "ENVIRONMENT"
    def env = System.getenv()
    env.each { k, v ->
        properties.setProperty(k, v)
    }

    main = mainClassName
    classpath = sourceSets.main.runtimeClasspath
    environment properties
}

